| Editor de bmr (BITMAPR)
| PHREDA 2010
|--------------------------------------------------
^r4/lib/gui.txt
^r4/lib/input.txt

^r4/lib/dlg.txt
^r4/lib/dlgfile.txt
^r4/lib/dlgcol.txt

^r4/lib/ricons.txt
^inc/ric/fontawesomewebfont.ric
^r4/lib/fonta.txt

^r4/lib/trazo.txt
^r4/lib/leebmp.txt
^r4/lib/bmr.txt

^r4/lib/trace.txt

#bmrm #bmrw #bmrh
#bmrww #bmrhh
#orden 0
#nrormt 0
#nomrmt )( 32
#modo

#color $ff

#zoom 2
#xi #yi | donde esta el pixel 0,0

#xv #yv	| aux para mover

:refreshbmr
	bmr.wh@
	dup 'bmrhh ! zoom << 'bmrh !
	dup 'bmrww ! zoom << 'bmrw !
	bmr.res@ 'bmrm !
	;

|---- dibujo en frame
::bmr@m | x y -- c
	2dup whin 0? ( 3drop ; ) drop | no obtiene
	yi - zoom >> swap xi - zoom >> swap
	bmr@ ;

:bmr!m32
	color rot rot
:bmr!m | c x y --
	2dup whin 0? ( 3drop ; ) drop | no dibuja afuera
	yi - zoom >> -? ( 3drop ; )
	swap xi - zoom >> -? ( 3drop ; )
	swap bmr! ;

|------ floodfill
#cf
#cc

:filltoh | x y -- x y y2
	2dup 
	( 1+ bmrhh <? )( | x y x yn
		2dup bmr@ cf <>? ( drop nip ; ) drop
		cc pick2 pick2 bmr!						
		) nip ;

:fillto0 | x y -- x y y1
	2dup 
	( 1- 0 >=? )( | x y x yn
		2dup bmr@ cf <>? ( drop nip ; ) drop
		cc pick2 pick2 bmr!						
		) nip ;

:fillcolorline | x y --
	2dup bmr@ cf <>? ( 3drop ; ) cc =? ( 3drop ; ) drop
	cc pick2 pick2 bmr!
	fillto0 1+ rot rot | y1 x y 
	filltoh 1- nip rot | x y2 y1 
	( over <? )( | x y2 yn
		pick2 1+ over fillcolorline
		pick2 1- over fillcolorline
		1+ ) 3drop ;
   

:bmrfill! | c x y --
	2dup whin 0? ( 4drop ; ) drop | no dibuja afuera
	yi - zoom >> -? ( 3drop ; )
	swap xi - zoom >> -? ( 3drop ; )
	swap 2dup bmr@ pick3 =? ( 4drop ; )
	'cf ! rot 'cc ! 
	fillcolorline ;

 
|*******************************
:ajusta
	bmr.wh@
	sh 2/ swap zoom << 2/ - 'yi !
	sw 2/ swap zoom << 2/ - 'xi !
    refreshbmr
	;

:drawbmr | --
	bmrm 1 and? ( bmrw bmrh xi yi drawalphagrid ) drop
	xi yi bmrw bmrh bmr.drawscalei
	bmrh dup 2/ yi + 'yc ! 2 + 'h !
	bmrw dup 2/ xi + 'xc ! 2 + 'w !
	blanco gc.box
	;

|-----------------------------------
:borrodib
	"Borro Dibujo ?" dlgsino
	1? ( bmr.clear ) drop
	;

|---- bmp
#bmp
#xa #ya #xb #yb
#wb #hb

:clearsel
	-1 'xa ! ;

:keybox | x y
    2dup hb + 'yb ! wb + 'xb ! 'ya ! 'xa ! ;

:modoseleccion
	[ xymouse keybox ; ]
	dup
	guiDnMove
	blink 1? ( blanco )( negro ) drop
	xa -? ( drop ; ) ya xb 1- yb 1- caja
	;

:getimagen
	xa -? ( drop ; )
	xb >? ( xb 'xa ! 'xb ! )( drop )
	ya yb >? ( yb 'ya ! 'yb ! )( drop )
	xb xa -	yb ya - bmr.wh!
	ajusta
	xa ( xb <? )(
		ya ( yb <? )(
		2dup bmp.get $ffffff and pick2 xa - pick2 ya - bmr!
		1+ ) drop
	1+ ) drop
	exit
	;

:cargabmps
	bmr.wh@ 'hb ! 'wb !
	clearsel
	33
	show clrscr
		0 0  bmp.xyreal
		modoseleccion
		blanco
		0 rows 3 - gotoxy
		dup "%d " print
		xa "x1:%d " print
		ya "y1:%d " print
		xb "x2:%d " print
		yb "y2:%d " print cr cr2
		cyan
		'getimagen dup <f1> "F1-GET" btnt
		'exit >esc<
		[ xa ya 1+ keybox ; ] <dn>
		[ xa ya 1- keybox ; ] <up>
		[ xa 1+ ya  keybox ; ] <ri>
		[ xa 1- ya keybox ; ] <le>
		cminiflecha ;

:cargabmp
	mark
	"media/bmp" dlgfileload 0? ( drop empty ; )
	bmp.load 'bmp !
	cargabmps
	empty
	;

|------------------------------
:modofill
	drawbmr
	[ color xymouse bmrfill! ; ] guiBtn
	;
:toofill
	;

|------------------------------
:addtrazo
	trazo.cnt 2 <? ( drop ; ) drop
	;

:mododraw
	drawbmr
	[ 'bmr!m32 vop! ; ] [ xymouse vline! ; ]  guiDnMove
	;

:toodraw
	violeta
	cyan
	'bmr.scrl "<" sp link
	'bmr.scrr ">" sp link
	'bmr.scru "^" sp link
	'bmr.scrd "v" sp link

	'bmr.flipv "-V-" sp link
	'bmr.fliph "-H-" sp link
|	'savebmr "SAVE" sp btnt
|	'exit "CANCEL" sp btnt
	;

|------EDIT
:pick.dn
	xymouse getpixel 'color ! ;

:modoedit
	drawbmr
	'pick.dn 'pick.dn guiDnMove
	; 

:tooedit
	cyan oscuro
	'cargabmp "BMP" sp btnt
	rojo oscuro
	'bmr.clear "CLS" sp btnt
	;

|--- modo vista
:vista.dn
	xymouse 'yv ! 'xv ! ;

:vista.move
	xymouse
	dup yv - 'yi +! 'yv !
	dup xv - 'xi +! 'xv !
	;

:bz+ zoom 1+ 6 >? ( 0 nip ) 'zoom ! ajusta refreshbmr ;
:bz- zoom 1- -? ( 6 nip ) 'zoom ! ajusta refreshbmr ;

:modovista
	drawbmr
	'vista.dn 'vista.move guiDnMove
	;
:toovista
	cyan oscuro
	'ajusta "CENTER" sp btnt
	'bz- "Z-" sp btnt
	'bz+ "Z+" sp btnt
	;

|------- modos
#modos 'mododraw 'modoedit 'modovista 'modofill
#tools 'toodraw 'tooedit 'toovista 'toofill

|----- botonera

#modo 0
#modogui 'mododraw
#modotoo 'toodraw

:setmodo
	dup 'modo !
	dup 2 <<
	dup 'modos + @ 'modogui !
	'tools + @ 'modotoo !
	;

:botonmodo | icon nro --
	modo =? ( azul )( negro )
	'setmodo btne
	modo =? ( blanco )( gris )
	drop
	8-gc drawric 8+gc gcdn ;

:modoactual
	modogui exec ;

:botonera
	48 qdim
	sw 26 - sh 32 - pos
	rojo 'exit dup >esc< btne blanco 'i.off drawric

	24 48 pos
	'i.pencil 0 botonmodo
	'i.edit 1 botonmodo
	'i.eye_open 2 botonmodo
	'i.magic 3 botonmodo

	modotoo exec

	24 qdim
	sw 26 - 48 pos
	'color btncol
	;

|---- cambia resolucion
:changew | dw
	bmr.wh@ swap rot + swap
	bmr.wh!
	ajusta ;

:changeh | dh
	bmr.wh@ rot +
	bmr.wh!
	ajusta ;

:changea
	bmr.res@ 1 xor bmr.res!
	refreshbmr ;

|-----------------------------
:main
	ajusta fonta-verdana14
	show clrscr
		modoactual

		verde
		[ 1 changew ; ] "+" link
		[ -1 changew ; ] "-" link
		blanco
		bmr.wh@ swap " %dx%d " print
		verde
		[ 1 changeh ; ] "+" link
		[ -1 changeh ; ] "-" link
		[ 16 dup bmr.wh! ajusta ; ] "16" link
		[ 32 dup bmr.wh! ajusta ; ] "32" link
		[ 64 dup bmr.wh! ajusta ; ] "64" link
		amarillo
|		'changea "ALPHA" sp link
		verde dup " - %d -" print

		botonera
		cminiflecha ;


:   33 mark
	"mem/notepad.bmr" bmr.load
	$fffff 'here +!
	refreshbmr
	main
	"mem/notepad.bmr" bmr.save
	;
