| :r4/menu
| PHREDA 2015
|------------------------
^r4/IDE/ibar.txt

^r4/lib/fontm.txt
^inc/fntm/droidsans13.txt

#nfiles
#files )( 8192
#files> 'files
#files< 'files
#filen )( $3fff
#filen> 'filen
#linesv 20
#nivel 0

#pagina 0
#actual 0
#path )( 1024
#name )( 64

|------------------- OS DEp ---------------------
:FNAME | adr -- adrname
|WIN|	44 +
|AND|	19 +
	;

:FDIR? | adr -- 1/0
|WIN|	@ $10
|AND|	18 + c@ 4
	=? ( 1 )( 0 ) nip ;
|----------------------------------------

:FINFO | adr -- adr info
	dup FDIR? 1? ( 0 nip ; ) drop
	dup FNAME
	".txt" =pos 1? ( 2drop 1 ; ) drop
	".bmr" =pos 1? ( 2drop 2 ; ) drop
	".inc" =pos 1? ( 2drop 3 ; ) drop
	".rmt" =pos 1? ( 2drop 4 ; ) drop
	".spr" =pos 1? ( 2drop 5 ; ) drop
	".vsp" =pos 1? ( 2drop 6 ; ) drop
	7 nip ;

:files.clear
	0 'nfiles !
	'filen 'filen> !
	'files dup 'files> ! 'files< !
	;

:files!+
	files> ( files< >? )(
		dup 4 - @ over !
		4 - ) drop
	files< !+ 'files< !
	4 'files> +!
	;

#maxm
:files.free
	0 'maxm !
	'files ( files> <? )(
		@+ maxm >? ( dup 'maxm ! ) drop ) drop
	maxm 8 >> 'filen +
	( c@+ 1? )( drop ) drop
	'filen> !
	;

:files.getpath
	'path
	ffirst drop | quita .
	fnext drop
	( fnext 1? )(
		FINFO nivel 4 << or filen> 'filen - 8 << or
		files!+
		FNAME filen> strcpyl 'filen> !
		) drop
	files> 'files - 2 >> 'nfiles !
	;

:getname | nro -- ""
	2 << 'files + @ 8 >> 'filen + ;

:getinfo | nro -- info
	2 << 'files + @ $ff and ;

:chginfo | nro --
	2 << 'files + dup @ $8 xor swap ! ;

:rebuild
	"r4" 'path strcpy
	files.clear
	0 'pagina !
	0 'nivel !
	files.getpath
	;

|-----------------------------
:t0 | nro i --
	verde
	4 >> 2* nsp "+ " print
	dup getname print "/" printx ;

:t8 | nro i --
	verde
	4 >> 2* nsp "- " print
	dup getname print "/" printx ;

:t1 | nro i --
	blanco
	4 >> 1+ 2* nsp
	dup getname print ;

#info 't0 't1 't1 't1 't1 't1 't1 't1 't8

:drawl
	actual =? ( gris linefill )
	dup getinfo
	dup $f and 2 << 'info + @ exec
	cr ;

:drawtree
	sw 3 / 'tx2 !
	0 ( linesv <? )( pagina +
		nfiles <? ( drawl )
		pagina - 1+ ) drop
	sw 'tx2 !
	;

|-----------------------------
:makepath | actual nivel --
	0? ( drop getname "r4/%s" mprint 'path strcpy ; )
	over 1-
	( dup getinfo 4 >> $f and pick2 >=? )( drop 1- ) drop
	over 1- makepath
	drop
	"/" 'path strcat
	getname 'path strcat
	;

:expande
	0 'name !
	actual
	dup getinfo 4 >> $f and makepath

   	actual chginfo
	actual getinfo 4 >> 1+ 'nivel !
    actual 1+ 2 << 'files + 'files< !
	files.getpath
	;

:remfiles
	actual chginfo
	actual getinfo $f0 and $10 +
	actual 1+
	( dup getinfo $f0 and pick2 =? )( drop 1+ ) drop
	nip
	actual 1+
	( swap nfiles <? )(
		dup 2 << 'files + @
		pick2 2 << 'files + !
		1+ swap 1+ ) drop
	2 << 'files + 'files> !
	files> 'files - 2 >> 'nfiles !
	files.free
	;

:contrae
	0 'name !
	'path ( c@+ 1? )( drop ) drop 1-
	( 'path >? )(
		dup c@ $2f =? ( drop 0 swap ! remfiles ; )
		drop 1- ) drop
	remfiles ;

|-------------
#pathaux )( 1024
#nameaux )( 64

:next/ | adr -- adr'
	( c@+ 1? )(
		$2f =? ( drop 0 swap 1- c!+ ; )
		drop ) nip ;

:getactual | adr actual --
	( nfiles <? )(
		dup getname pick2 =s 1? ( drop nip ; )
		drop 1+ ) nip ;

:reload
	'path 'pathaux strcpy
	'name 'nameaux strcpy
	rebuild
	'pathaux c@ 0? ( drop ; ) drop
	0 'actual !
	'pathaux next/
	( dup next/ swap | sig act
		actual getactual 'actual !
		expande
		0? ) drop
	'nameaux
	dup c@ 0? ( 2drop ; ) drop
	actual getactual 'actual !
	pagina linesv + 1- >=? ( dup linesv - 1+ 'pagina ! ) drop
	;

:loadm
	'path 1024 64 + "mem/menu.mem" load
	'path =? ( drop ; ) drop
	reload
	;

:savem
	'path 1024 64 + "mem/menu.mem" save ;

:enter
	actual getinfo $f and
	0? ( drop expande ; )
	8 =? ( drop contrae ; )
	drop
	actual getname 'name strcpy
	;

:fdn
	actual nfiles 1- >=? ( drop ; )
	1+ pagina linesv + 1- >=? ( dup linesv - 1+ 'pagina ! )
	'actual !
	;

:fup
	actual 0? ( drop ; )
	1- pagina <? ( dup 'pagina ! )
	'actual !
	;

:main
	4
	show clrscr
		gris linefill
		blanco
		'path print sp
		'name print	cr

		drawtree
		ibar

        'reload <f1>
		'fdn <dn>
		'fup <up>
		'enter <enter>

		'exit >esc<
		cminiflecha ;

:ram
	mark
	'fontdroidsans13 fontm
	clrscr rows 3 - 'linesv !
	rebuild
	loadm
	ibar.ini
	;

: ram main savem ;