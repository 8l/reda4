|
|
|-----------------------------
^r4/lib/gui.txt
^r4/lib/img.txt

^r4/lib/loadjpg.txt
^r4/lib/loadpng.txt
^r4/lib/loadbmp.txt

^r4/lib/trace.txt

^r4/lib/rfont.txt
^../r4/inc/rft/droidsansbold.rft

#recarga 0

#videos
#videonow
#placas
#placasnow

#marquesina
#tshadow $ffffff

#img1

|------------------------------------------------
:eventocheck

	;

|------------------- OS DEp ---------------------
:FNAME | adr -- adrname
|WIN|	44 +
|AND|	19 +
	;

:FDIR? | adr -- 1/0
|WIN|	@ $10
|AND|	18 + c@ 4
	=? ( 1 )( 0 ) nip ;

|------------------------------------------------
:addfile | adr --
	dup FDIR? 1? ( 2drop ; ) drop
	FNAME here strcpyl 'here ! ;

:getfilenames | path -- files
	here swap
	ffirst drop fnext drop | quita . y ..
	( fnext 1? )( addfile ) drop
	0 ,c ;

|-----------------------------------------------
:playffm | "" --
	"atv/ffplay -fs -autoexit -loglevel quiet ""atv/videos/%s""" mprint
	system drop ;

:waitvideo
	( -1 system -1 <>? )( drop
		eventocheck
		100 update drop ) drop ;

:waitvideo
	show
		'exit >esc<
		-1 system -1 =? ( exit 0 system drop ) drop ;

:playvideo
	videonow playffm
	videonow >>0 dup c@ 0? ( 2drop videos )( drop ) 'videonow !
 	;

:player
	-1 system -1 <>? ( drop ; ) drop
	playvideo ;

|------------------------------------------------
:sprint | "" --
	mprint
	ccx ccy ink@
	2 2 +atxy pick3
	tshadow ink printx
	ink atxy
	printx ;

|------------------------------------------------
:loadimg | filename -- img
	placasnow
	dup ".jpg" =pos 1? ( drop "atv/placas/%s" mprint loadjpg ; ) drop
	dup ".png" =pos 1? ( drop "atv/placas/%s" mprint loadpng ; ) drop
	dup ".bmp" =pos 1? ( drop "atv/placas/%s" mprint loadbmp ; )
	2drop 0 ;

:nextplaca
	placasnow >>0 dup c@ 0? ( 2drop placas )( drop ) 'placasnow ! ;

#placaimg

:playcartelpre
	( loadimg 0? )( drop nextplaca ) 'placaimg !
	nextplaca ;

:playcartel
	0 0 sw sh placaimg img.drawsize
|	0 0 placaimg img.draw
	>xfb
	;

|----------------------------------------
:fadeout1
	framev >r
	sw sh * ( 1? )( 1-
		r @ 2/ $7f7f7f and r!+ ) drop
	rdrop ;

:fadeout
	8 ( 1? )( 1-
		fadeout1
		30 update drop
		redraw
		) drop ;

|----------------------------------------
:fadein1 | nro -- nro
	xfb
	framev >r
	sw sh * ( 1? )( 1- swap
		@+ pick3 >> $10101 and
		r @ 2* or r!+
		swap ) 2drop
	rdrop ;

:fadein
	cls
	8 ( 1? )( 1-
		fadein1
		30 update drop
		redraw
		) drop ;

|----------------------------------------

#xm 0
:frente
	xm sh 90 - atxy
	rojo
	marquesina sprint
	-3 'xm +!
	;

:placa1
	sw 'xm !
	0 0 sw sh img1 img.drawsize >xfb
	.restart
	show xfb>scr
		frente
		'exit >esc<
		8000 .mseg exit
		;

|---------------
:info
	-1 system nip
	-1 =? ( "cartel" )( "video" )
	print cr
	;


:reread
	recarga 1? ( empty ) drop
	1 'recarga !
	mark
	"atv/videos/" getfilenames 'videos !
	"atv/placas/" getfilenames 'placas !

	videos 'videonow !
	placas 'placasnow !

	;

:dump
	"videos" print cr
	videos ( dup c@ 1? )( drop
		dup print cr
		>>0 ) 2drop
	cr
	"placas" print cr
	placas ( dup c@ 1? )( drop
		dup print cr
		>>0 ) 2drop
	;

|--------------------------------------
#timestr )( 16

:hms
	10 <? ( "0" ,s ) ,d ":" ,s
	10 <? ( "0" ,s ) ,d ":" ,s
	10 <? ( "0" ,s ) ,d ;

:hmss
	10 <? ( "0" ,s ) ,d
	swap 1 and? ( ":" )( " " ) nip ,s
	10 <? ( "0" ,s ) ,d ;

:reloj
	mark 'timestr 'here !
	time | s m h
	hmss
	0 ,c empty

	'timestr sizeprint 8 +
	sw swap - sh cch - 4 - atxy
	negro print
	;

|--------------------------------------

:main
	playcartelpre playcartel fadein
	33
	show xfb>scr
|		dup "%d" sprint
		reloj
|		info
|		playvideo
|		player

		[ playcartelpre fadeout playcartel fadein ; ] <f1>
		'exit >esc<
		cminiflecha ;

:ini
	mark
	reread
|	here dup 'marquesina !
|	"marquesina.txt" load 'here !
	droidsansbold 80 rfont!
	;

: ini main ;